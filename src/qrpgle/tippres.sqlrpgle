**FREE
CTL-OPT NOMAIN OPTION(*NODEBUGIO:*SRCSTMT:*NOUNREF);

//***********************************************************************
// PARA COMPILAR:
//
// CRTSQLRPGI OBJ(JBUSTOS2/TIPPRES) SRCFILE(JBUSTOS2/QRPGLESRC)
// SRCMBR(TIPPRES) COMMIT(*NONE) OBJTYPE(*MODULE) OPTION(*EVENTF)
// CLOSQLCSR(*ENDMOD) REPLACE(*YES) DBGVIEW(*SOURCE)
//
// CRTSRVPGM SRVPGM(JBUSTOS2/SRVPRE) MODULE(JBUSTOS2/TIPPRES)
// EXPORT(*ALL)  SRCFILE(JBUSTOS2/QSRVSRC) SRCMBR(SRVPRE)
// TEXT('PROGRAMA SERVICIOS PRESTAMOS') ALWLIBUPD(*YES)
//
// CREAR DIRECTORIO
// CRTBNDDIR BNDDIR(BABEL/BNDDIRBAB)
// TEXT('Directorio de Servicios Sistema de Prestamos')
//
// AGREGAR A DIRECTORIO
// ADDBNDDIRE BNDDIR(JBUSTOS2/BNDDIRBAB) OBJ((JBUSTOS2/SRVPRE))
//************************************************************************

// TRAMA DE ENTRADA
//ESTRUCTURA DE DATOS DE ENTRADA
DCL-DS TRTIPPRESEN QUALIFIED; //TRAMA TIPO PRESTAMO ENTRADA
    ACCION   CHAR(2);
    TIPOPRE  ZONED(3);
    NOMBRE   CHAR(30);
    TASA     ZONED(6:4);
    PLAZO    ZONED(3);
    KEY      CHAR(10);
END-DS;

// TRAMA DE SALIDA
//ESTRUCTURA DE DATOS DE SALIDA
DCL-DS TRTIPPRESSA QUALIFIED;
    RESPUESTA  CHAR(2);
    MENSAJE    CHAR(100);
    TIPOPRE    ZONED(3);
    NOMBRE     CHAR(30);
    TASA       ZONED(6:4);
    PLAZO      ZONED(3);
END-DS;

DCL-DS LISTATIPPRES QUALIFIED;
    RESPUESTA  CHAR(2);
    MENSAJE    CHAR(100);
    CANTIDAD INT(3);
    DCL-DS TIPPRES DIM(100);
            TIPOPRE    ZONED(3);
            NOMBRE     CHAR(30);
            TASA       ZONED(6:4);
            PLAZO      ZONED(3);
    END-DS;
END-DS;

// DEFINICION DE PROTOTIPOS
// PROTOTIPO DE TIPO DE PRESTAMO
/COPY JBUSTOS2/QTXTSRC,TIPPRES

//IMPLEMENTACION
// INICIO DEL PROCEDIMIENTO PROCESA TIPO PRESTAMO
DCL-PROC PROCESA_TIPPRES EXPORT;
    //INTERFAZ DE PROCEDIMIENTO PROCEDA TIPO PRESTAMO
    DCL-PI *N;
        PBIB   VARCHAR(10);
        PCOLA  VARCHAR(10);
        PTRAMA LIKEDS(TRAMA);
    END-PI;

    //FIN DE INTERFAZ DE PROCEDIMIENTO
    DCL-S RESPUESTA VARCHAR(118); //VARIABLE INDEPENDIENTE RESPUESTA
    TRTIPPRESEN = PTRAMA;

    IF TRTIPPRESEN.ACCION = 'CT';
        RESPUESTA = VALIDATIPPRES(TRTIPPRESEN);
        IF %SUBST(RESPUESTA:1:2) = '00';
            RESPUESTA = GRABATIPPRES(TRTIPPRESEN);
        ENDIF;
    ELSEIF TRTIPPRESEN.ACCION = 'DT';
        RESPUESTA = BORRARTIPPRES(TRTIPPRESEN);
    ELSEIF TRTIPPRESEN.ACCION = 'GT';
        RESPUESTA = TRAERTIPPRES(TRTIPPRESEN);
    ELSEIF TRTIPPRESEN.ACCION = 'MT';
        RESPUESTA = VALIDATIPPRES(TRTIPPRESEN);
        IF %SUBST(RESPUESTA:1:2) = '00';
            RESPUESTA = MODIFICARTIPPRES(TRTIPPRESEN);
        ENDIF;
    ELSEIF TRTIPPRESEN.ACCION = 'LT';
        RESPUESTA = LISTARTIPPRES();
    ENDIF;

    EXEC SQL CALL QSYS2.SEND_DATA_QUEUE(
        MESSAGE_DATA =>:RESPUESTA, DATA_QUEUE => :PCOLA,
        DATA_QUEUE_LIBRARY => :PBIB, KEY_DATA => :TRTIPPRESEN.KEY);
END-PROC;
//FIN DEL PROCEDIMIENTO PROCESA TIPO PRESTAMO

DCL-PROC VALIDATIPPRES;
    DCL-PI *N VARCHAR(118);
            TRAMA LIKEDS(TRTIPPRESEN);
    END-PI;
    DCL-DS TRAMASAL LIKEDS(TRTIPPRESSA);  //VARIABLE INDEPENDIENTE TRAMA SALIDA
    TRAMASAL.RESPUESTA = '00';
    IF %LEN(%TRIM(TRAMA.NOMBRE)) = 0;
        TRAMASAL.RESPUESTA = '02';
        TRAMASAL.MENSAJE = 'NOMMBRE DE TIPO DE PRESTAMO NO PUEDE ESTAR EN BLANCO';
    ELSEIF TRAMA.TASA = 0;
        TRAMASAL.RESPUESTA = '03';
        TRAMASAL.MENSAJE = 'TASA NO PUEDE SER CERO';
    ELSEIF TRAMA.PLAZO = 0;
        TRAMASAL.RESPUESTA = '04';
        TRAMASAL.MENSAJE = 'PLAZO NO PUEDE SER CERO';
    ENDIF;
    RETURN TRAMASAL;
END-PROC;


// INICIO DEL PROCEDIMIENTO GRABA TIPO PRESTAMO
DCL-PROC GRABATIPPRES;
    //INTERFAZ DE PROCEDIMIENTO GRABA TIPO PRESTAMO
    DCL-PI *N CHAR(118);
        TRAMA LIKEDS(TRTIPPRESEN);
    END-PI;

    //FIN INTERFAZ GRABA TIPO PRESTAMO
    DCL-S CUANTOS INT(3); //VARIABLE INDEPENDIENTE CONTADOR
    DCL-DS TRAMASAL LIKEDS(TRTIPPRESSA);//VARIABLE INDEPENDIENTE TRAMA SALIDA
    //VALIDAR QUE NO EXISTA EL TIPO PRESTAMO
    EXEC SQL SELECT COUNT(1) INTO :CUANTOS
            FROM TIPPRES
            WHERE TIPOPRE=:TRAMA.TIPOPRE;
    IF CUANTOS>0;
        TRAMASAL.RESPUESTA  ='ER';
        TRAMASAL.MENSAJE='CODIGO DE TIPO DE PRESTAMO YA EXISTE';
    ELSE;
        EXEC SQL INSERT INTO TIPPRES(TIPOPRE,NOMBRE, TASA, PLAZO,
                                    USUCREO,FECCREO)
                VALUES(:TRAMA.TIPOPRE,:TRAMA.NOMBRE, :TRAMA.TASA,
                        :TRAMA.PLAZO, CURRENT_USER, CURRENT_DATE);
        TRAMASAL.RESPUESTA='00';
        TRAMASAL.MENSAJE='SE CREO EL TIPO DE PRESTAMO';
    ENDIF;
    RETURN TRAMASAL;
END-PROC;
//FIN DEL PROCEDIMIENTO GRABA TIPO PRESTAMO

// INICIO DEL PROCEDIMIENTO BORRAR TIPO PRESTAMO
DCL-PROC BORRARTIPPRES;
    //INTERFAZ DE PROCEDIMIENTO BORRAR TIPO PRESTAMO
    DCL-PI *N CHAR(118);
        TRAMA LIKEDS(TRTIPPRESEN);
    END-PI;
    //FIN INTERFAZ GRABA TIPO PRESTAMO
    DCL-S CUANTOS INT(3); //VARIABLE INDEPENDIENTE CONTADOR
    DCL-DS TRAMASAL LIKEDS(TRTIPPRESSA);//VARIABLE INDEPENDIENTE TRAMA SALIDA

    //VALIDAR QUE NO EXISTA EL TIPO PRESTAMO
    EXEC SQL SELECT COUNT(1) INTO :CUANTOS
                FROM TIPPRES
                WHERE TIPOPRE=:TRAMA.TIPOPRE;
    IF CUANTOS=0;
        TRAMASAL.RESPUESTA  ='05';
        TRAMASAL.MENSAJE='CODIGO DE TIPO DE PRESTAMO NO EXISTE';
    ELSE;
        EXEC SQL DELETE FROM TIPPRES
                WHERE TIPOPRE=:TRAMA.TIPOPRE;

        TRAMASAL.RESPUESTA='00';
        TRAMASAL.MENSAJE='SE ELIMINO EL TIPO DE PRESTAMO';
    ENDIF;
    RETURN TRAMASAL;
END-PROC;
//FIN DEL PROCEDI

// INICIO DEL PROCEDIMIENTO TRAER TIPO PRESTAMO
DCL-PROC TRAERTIPPRES;
    //INTERFAZ DE PROCEDIMIENTO TRAER TIPO PRESTAMO
    DCL-PI *N CHAR(118);
            TRAMA LIKEDS(TRTIPPRESEN);
    END-PI;
    //FIN INTERFAZ TRAER TIPO PRESTAMO
    DCL-S CUANTOS INT(3); //VARIABLE INDEPENDIENTE CONTADOR
    DCL-DS TRAMASAL LIKEDS(TRTIPPRESSA);//VARIABLE INDEPENDIENTE TRAMA SALIDA

    //VALIDAR QUE NO EXISTA EL TIPO PRESTAMO
    EXEC SQL SELECT COUNT(1) INTO :CUANTOS
                FROM TIPPRES
                WHERE TIPOPRE=:TRAMA.TIPOPRE;
    IF CUANTOS=0;
        TRAMASAL.RESPUESTA  ='05';
        TRAMASAL.MENSAJE='CODIGO DE TIPO DE PRESTAMO NO EXISTE';
    ELSE;
        EXEC SQL SELECT NOMBRE, TASA, PLAZO
                INTO   :TRAMASAL.NOMBRE,:TRAMASAL.TASA,
                        :TRAMASAL.PLAZO
                FROM TIPPRES
                WHERE TIPOPRE=:TRAMA.TIPOPRE;

        TRAMASAL.RESPUESTA='00';
        TRAMASAL.MENSAJE='SE TRAJO LA INFORMACIÃ“N DEL TIPO DE PRESTAMO';

    ENDIF;
    RETURN TRAMASAL;
END-PROC;
//FIN DEL PROCEDIMIENTO TRAER TIPO PRESTAMO      MIENTO BORRAR TIPO PRESTAMO

//INICIO DEL PROCEDIMIENTO MODIFICAR TIPO PRESTAMO
DCL-PROC MODIFICARTIPPRES;
    //INTERFAZ DE PROCEDIMIENTO MODIFICAR TIPO PRESTAMO
    DCL-PI *N CHAR(118);
        TRAMA LIKEDS(TRTIPPRESEN);
    END-PI;
    //FIN INTERFAZ MODIFICAR TIPO PRESTAMO
    DCL-S CUANTOS INT(3); //VARIABLE INDEPENDIENTE CONTADOR
    DCL-DS TRAMASAL LIKEDS(TRTIPPRESSA);//VARIABLE INDEPENDIENTE TRAMA SALIDA

    //VALIDAR QUE NO EXISTA EL TIPO PRESTAMO
    EXEC SQL SELECT COUNT(1) INTO :CUANTOS
                FROM TIPPRES
                WHERE TIPOPRE=:TRAMA.TIPOPRE;
    IF CUANTOS=0;
        TRAMASAL.RESPUESTA  ='05';
        TRAMASAL.MENSAJE='CODIGO DE TIPO DE PRESTAMO NO EXISTE';
    ELSE;
        EXEC SQL UPDATE TIPPRES
                SET NOMBRE=:TRAMA.NOMBRE,
                    TASA=:TRAMA.TASA,
                    PLAZO=:TRAMA.PLAZO
                WHERE TIPOPRE=:TRAMA.TIPOPRE;

        TRAMASAL.RESPUESTA='00';
        TRAMASAL.MENSAJE='SE MODIFICO EL TIPO DE PRESTAMO';

    ENDIF;
    RETURN TRAMASAL;
END-PROC;
//FIN DEL PROCEDIMIENTO MODIFICAR TIPO PRESTAMO

DCL-PROC LISTARTIPPRES;
    //INTERFAZ DE PROCEDIMIENTO TRAER TIPO PRESTAMO
    DCL-PI *N CHAR(5000);
    END-PI;
    //FIN INTERFAZ TRAER TIPO PRESTAMO

    DCL-DS TRAMASAL LIKEDS(LISTATIPPRES);  //VARIABLE INDEPENDIENTE TRAMA SALIDA
    DCL-S TIPOPRE ZONED(3);
    DCL-S NOMBRE VARCHAR(30);
    DCL-S TASA ZONED(6:4);
    DCL-S PLAZO ZONED(3);
    DCL-S $EXIT IND;
    DCL-S I INT(3);
    DCL-S CUANTOS INT(3); //VARIABLE INDEPENDIENTE CONTADOR

    //VALIDAR QUE NO EXISTA EL TIPO PRESTAMO
    EXEC SQL SELECT COUNT(1) INTO :CUANTOS
                FROM TIPPRES;
    TRAMASAL.CANTIDAD=CUANTOS;
    IF TRAMASAL.CANTIDAD=0;
        TRAMASAL.RESPUESTA  ='01';
        TRAMASAL.MENSAJE='NO HAY TIPOS DE PRESTAMOS DEFINIDOS';
    ELSE;
        EXEC SQL DECLARE C1 INSENSITIVE CURSOR FOR
            SELECT TIPOPRE, NOMBRE, TASA,PLAZO
            FROM TIPPRES
            FOR READ ONLY;

        EXEC SQL OPEN C1;
        $EXIT=*OFF;
        I=1;
        DOW $EXIT=*OFF;
            EXEC SQL FETCH C1 INTO  :TIPOPRE, :NOMBRE, :TASA, :PLAZO;

            IF SQLCODE=0 AND I<=100;
                TRAMASAL.TIPPRES(I).TIPOPRE=TIPOPRE;
                TRAMASAL.TIPPRES(I).NOMBRE =NOMBRE;
                TRAMASAL.TIPPRES(I).TASA=TASA;
                TRAMASAL.TIPPRES(I).PLAZO=PLAZO;
                I=I+1;
            ELSEIF SQLCODE=100;
                $EXIT=*ON;
            ELSE;
                $EXIT=*ON;
            ENDIF;
        ENDDO;
        EXEC SQL CLOSE C1;

        TRAMASAL.RESPUESTA='00';
        TRAMASAL.MENSAJE='SE EXTRAJO CON EXITO TODOS LOS TIPOS DE PRESTAMOS';

    ENDIF;
    RETURN TRAMASAL;
END-PROC;
